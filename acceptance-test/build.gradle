buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.yaml:snakeyaml:1.21'
        classpath 'org.owasp:dependency-check-gradle:3.3.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'project-report'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'

group 'org.zalando'
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    acceptanceTest {
        java {
            srcDir file('src/acceptance-test/java')
        }
        resources.srcDir file('src/acceptance-test/resources')
    }
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
}

findbugs {
    reportLevel = "high"
    sourceSets = []
}

dependencies {
    ext {
        dropwizardVersion = '3.1.3'
        curatorVersion = '4.2.0'
        zookeeperVersion = '3.4.14'
        jacksonVersion = '2.9.8'
        springFrameworkVersion = '4.3.23.RELEASE'
    }
    // Override spring-boot BOM versions
    ext['json.version'] = '20180130'
    ext['json-path'] = '2.4.0'
    ext['jsonassert'] = '1.5.0'

    compile project(':core-common')
    compile project(':core-metastore')
    compile project(':core-services')

    compile project(':api-publishing')
    compile project(':api-consumption')
    compile project(':api-metastore')
    compile project(':api-cursors')
    compile project(':api-misc')
    compile project(':app')

    // spring
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'logback-classic'
        exclude module: 'log4j-over-slf4j'
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile "org.springframework:spring-context:$springFrameworkVersion"
    compile "org.springframework:spring-web:$springFrameworkVersion"
    compile "org.springframework:spring-webmvc:$springFrameworkVersion"
    compile "org.springframework.boot:spring-boot-starter-jetty"

    compile('org.springframework.boot:spring-boot-starter-security') {
        exclude module: "logback-classic"
    }

    compile 'org.slf4j:slf4j-log4j12'

    compile("com.fasterxml.jackson.datatype:jackson-datatype-json-org:$jacksonVersion") {
        exclude module: "json"
    }
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion"
    compile 'org.zalando:jackson-datatype-problem:0.22.0'
    compile 'org.zalando:problem:0.22.0'
    compile 'org.json:json:20180130'

    compile('org.zalando.stups:stups-spring-oauth2-server:1.0.22') {
        exclude module: "httpclient"
    }
    compile 'org.zalando:twintip-spring-web:1.1.0'

    // tests
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile('org.junit.jupiter:junit-jupiter-api:5.5.2') {
        exclude module: "hamcrest-core"
    }
    testCompile "org.springframework:spring-test"
    testCompile 'org.springframework.boot:spring-boot-test'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.skyscreamer:jsonassert'
    testCompile 'uk.co.datumedge:hamcrest-json:0.2'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile('com.jayway.restassured:rest-assured:2.9.0') {
        exclude module: "hamcrest-core"
        exclude module: "hamcrest-library"
    }
    testCompile 'com.jayway.jsonpath:json-path'
    testRuntime 'org.pegdown:pegdown:1.6.0'
    testCompile project(path: ':core-common', configuration: 'testOutput')
    testCompile project(path: ':core-services', configuration: 'testOutput')
}
// end::dependencies[]

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task acceptanceTest(type: Test) {
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
}

acceptanceTest {
    testLogging {
        events "passed", "skipped", "failed"
    }
    testLogging.exceptionFormat = 'full'
    maxParallelForks = 1
}

bootRepackage {
    enabled = false
}
